# SPDX-FileCopyrightText: 2015,2017-2019,2021,2023-2025 Einhard Leichtfuß <alguien@respiranto.de>
# SPDX-License-Identifier: GPL-3.0-or-later

# Maintainer: Einhard Leichtfuß <alguien@respiranto.de>
# Contributor: Tai Chi Minh Ralph Eastwood <tcmreastwood@gmail.com>
pkgname=dict-gcide
_major_debver=0.48
_debver="${_major_debver}.5+nmu4"
pkgver=0.54
pkgrel=3
pkgdesc="GNU version of the Collaborative International Dictionary of English for dictd et al."
arch=('any')
url="https://gcide.gnu.org.ua/"
license=('GPL-3.0-or-later')
optdepends=('dictd: dict client and server')
makedepends=('dictd' 'libmaa')
provides=('dictd-gcide')
conflicts=('dictd-gcide')
install="${pkgname}.install"
source=('fixes.sed'
        'post_webfilter.sed'
        'check.sed'
        "https://deb.debian.org/debian/pool/main/d/${pkgname}/${pkgname}_${_debver}.tar.xz"
        "https://ftp.gnu.org/gnu/gcide/gcide-${pkgver}.tar.xz"{,.sig})
sha512sums=('0f566251255388691c936f52e7e57489ad01ce1901a90ddffb11072e24353a329e7369f6dba8cc4c42c4ce08b414df4abb0aad93becad9afe05f5138f92be38d'
            'ecbe9834f7eaac8ea72a27fc6821d55c0d44da942aea203e7db2b4feb6a0cdcab2c9f08270680a63a533ef42e1bac60a02cc4f5ef94ddc09c1adab9aa299ad31'
            '70817060470e14298a58f326db106604b31499eecfd71fe6c0298941f654692d40dc8f2d0e79f73ebd1bb1579b471a51e3a7ae219d3268d03b751f7e24dfa215'
            '71134c69ece86226233c0693af1d8c776df469bbf3d4969f4a8a883061da2e23eafda2ad395648350ee3fe56f271abd20d7d3fd37c34f0401200317ed1a249be'
            '9bda8bc2e30a529bafeb3fcdd2f315025209fa2e609da707caf7b4a273221a7617a10b58d2b635e1ae980e01a790a4e09bb74ec54d6e09c9014e72b30d33b1e6'
            'SKIP')
validpgpkeys=('4BE4E62655488EB92ABB468F79FFD94BFCE230B1')


prepare()
{
	cd "$pkgname"

	sed -Ei \
		"s/\"(The Collaborative International Dictionary of English) v.${_major_debver}\"/\"\\1 v.${pkgver}\"/" \
		scan.l

	# Remove autogenerated autotools files.
	rm config.guess config.h.in config.sub configure install-sh
}


build()
{
	cd "$pkgname"

	autoreconf -fis
	./configure
	make -j1

	# Do the conversion explicitly, instead of `make db', to account for all
	# the differences to the original build process.
	# LANG=C is required so that the index file is properly sorted.
	../fixes.sed "../gcide-${pkgver}"/CIDE.? \
		| sed -f debian/sedfile \
		| ./webfilter \
		| ../post_webfilter.sed \
		| tee pre_webfmt.data \
		| LANG=C ./webfmt -c

	# `dictzip -v' neglects to print a final newline.
	dictzip -v gcide.dict
	printf '\n'
}


check()
{
	errors="$(./check.sed < "${pkgname}/pre_webfmt.data")"

	if test -n "$errors"
	then
		printf 'Errors found:\n'
		printf '%s\n' "$errors"
		return 1
	fi
}


package()
{
	install -m 0755 -d "${pkgdir}/usr/share/dictd"
	install -m 0644 -t "${pkgdir}/usr/share/dictd/" \
		"${pkgname}"/gcide.{dict.dz,index}

	install -m 0755 -d "${pkgdir}/usr/share/doc/dict-gcide"
	install -m 0644 -t "${pkgdir}/usr/share/doc/dict-gcide/" \
		"gcide-${pkgver}"/{README,INFO,pronunc.txt}
}
